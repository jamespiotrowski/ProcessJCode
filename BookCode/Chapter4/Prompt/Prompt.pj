import std.*;

void writer(chan<int>.write out){
    timer t;
    long w;
    for(int i = 0; i < 10; i++){
        w = t.read() + 10;
        t.timeout(w);
        out.write(i);
    }
    println("Writer done");
}

void reader(chan<int>.read in){
    int x;    
    timer t;
    long w;
    for(int i = 0; i < 100; i++){
        w = t.read() + 2;
        t.timeout(w);
        x = in.read();
        println("Read: " + x);
    }
}

public void memcell(chan<int>.read in, chan<int>.write out, chan<boolean>.read request) {
    int x = -947;
    while (true) {
        boolean any;
        alt {
            x = in.read()        : skip;
            any = request.read() : out.write(x);
        }
    }
}

public void prompt(chan<boolean>.write request, chan<int>.read in, chan<int>.write out) {
    int x;
    while (true) {
        request.write(true);
        x = in.read();
        out.write(x);
    }
} 

public void main(string[] args){
    chan<int> c1, c2, c3;
    chan<boolean> r;
    par{
        writer(c1.write);
        memcell(c1.read, c2.write, r.read);
        prompt(r.write, c2.read, c3.write);
        reader(c3.read);
    }
}
